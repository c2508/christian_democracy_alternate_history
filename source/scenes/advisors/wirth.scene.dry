title: Joseph Wirth
is-pinned-card: true 
tags: advisor, left_labor
card-image: img/portraits/WirthJoseph.jpg
new-page: true
view-if: wirth_advisor = 1

= Joseph Wirth

Wirth is a former chancellor and figure of the Zentrum's left wing who has been devotedly republican, to the extent of angering his former party members. He is a strong supporter of maintaining democracy and cooperation with the <span style="color: #c00000;">**SPD**</span>.

- @spd_cooperation
- @build_reichsbanner
- @democratic_rally
- @snap_election
- @root: Return to main

@spd_cooperation
title: Cooperation with the <span style="color: #c00000;">**SPD**</span>
subtitle: Push the party to be more friendly towards the <span style="color: #c00000;">**SPD**</span>.
unavailable-subtitle: [? if advisor_action_timer > 0 : [+ advisor_action_timer +] months before next advisor action. ?]
choose-if: advisor_action_timer = 0
on-arrival: advisor_action_timer = 6; left_labor_strength += 5; spd_relation += 5*(1-dissent); right_dissent += 5; right_labor_dissent +=3; left_labor_dissent -= 1

Wirth was in the SPD before and is able to use his close ties with <span style="color: #c00000;">**SPD**</span> members to deepen cooperation between both parties. 

@build_reichsbanner
title: Reichsbanner
subtitle: Wirth supports providing more resources to the Reichsbanner.
choose-if: advisor_action_timer = 0; z_rb = 1; z_endorse_rb = 1;
unavailable-subtitle: [? if advisor_action_timer > 0 : [+ advisor_action_timer +] months before next advisor action. ?][? if z_rb = 0 : The Center Party is not part of the Reichsbanner. ?][? if z_endorse_rb = 0 : The Center Party does not officially endorse the Reichsbanner. ?]
on-arrival: advisor_action_timer = 6; reichsbanner_timer -= 6; last_advisor_action = 1
go-to: reichsbanner 

@democratic_rally
title: Rally for democracy
subtitle: Rally our party and the German people to defend democracy and freedom.
choose-if: advisor_action_timer == 0 
unavailable-subtitle: [? if advisor_action_timer > 0 : [+ advisor_action_timer +] months before next advisor action. ?]
on-arrival: advisor_action_timer = 6; pro_republic += 5; democratization += 1; left_labor_strength += 5; right_dissent += 2; workers_spd += 4*(1-dissent) if democratization >= 4; new_middle_spd += 3*(1-dissent) if democratization >= 4

Wirth helps us to rally the people in defense of democracy.

@snap_election
title: Snap Election
subtitle: Call a snap election.
unavailable-subtitle: [? if chancellor != "Wirth" and president != "Wirth" : Wirth must be Chancellor or President. ?][? if advisor_action_timer > 0 : [+ advisor_action_timer +] months before next advisor action. ?] [? if president = "Wirth" and presidential_powers : We have removed this power from the president with a constitutional reform. ?]
choose-if: advisor_action_timer == 0 and (chancellor == "Wirth" or (president == "Wirth" and not presidential_powers))
on-arrival: advisor_action_timer = 6; next_election_time = time + 3 if next_election_time > time+3; next_election_month = month + 3 if next_election_time >= time+3; next_election_year = year if next_election_time >= time+3; next_election_year += 1 if next_election_month > 12; next_election_month -= 12 if next_election_month > 12; 

We have called for new elections, set for [+ next_election_month : month +] [+ next_election_year +].






