title: Media
new-page: true
is-card: true
tags: party_affairs
view-if: media_timer <= 0
on-arrival: month_actions += 1; media_timer += 6;
card-image: img/sangerbund.jpg
#subtitle: Setting the direction for our newspapers and radio.

= Media

The Centre Party wields much influence in Germany's media scene, with control over many local and regional newspapers throughout every German city and region. We could use the media for various purposes...

- @commercial
- @campaign
- @ideological
- @newspaper 
- @newspaper_2
- @newspaper_3
- @newspaper_4
- @return: We should not do anything with media at the moment.
- @easy_discard
- @cancel_advisor_action

@commercial
title: Make our newspapers more commercialized by broadening their appeal beyond party supporters.
subtitle: This might upset ideological purists, but it may bring in more funds and perhaps expose non Catholics to Catholic views.
on-arrival: resources += 1; commercialized_media += 1; new_middle_z += 4*(1 - dissent); old_middle_z += 3*(1-dissent); left_labor_dissent += 10; right_dissent += 10

Our commercialized newspapers are able to bring in more funds. However, the left labor and right factions express their displeasure.

@campaign
title: The purpose of our media is to elect more Catholics.
subtitle: Our newspapers will focus on the electoral campaign.
on-arrival: campaign_media += 1; catholics_z += 5*(1 - dissent)

Our campaign media is mostly reaching the Catholics who are already saturated with Catholic propaganda, but it must be doing *something*, or at least that is what we hope.

@ideological
title: We will continue to follow the official party line regarding important issues. 
subtitle: This helps prevent fragmentation amongst the party and preserves party unity.
on-arrival: left_dissent -= 6; left_labor_dissent -= 6; center_dissent -= 6; right_labor_dissent -= 6; right_dissent -= 6; young_conservatives_dissent -= 6

This has been our media course historically, and it has successfully paved over the many tensions in our party.

@newspaper
title: Allocate funds to Germania
subtitle: -1 resource
choose-if: resources >= 1
unavailable-subtitle: Not enough resources.
view-if: newspaper = 0
on-arrival: newspaper += 1; resources -= 1; new_middle_z += 3*(1 - dissent); old_middle_z += 3*(1 - dissent); catholics_z += 2*(1 - dissent); pro_republic += 3*(1-dissent) if democratization > 1

Our expanded newspaper allows us to connect to people in a more intimate setting than before, spreading the message of political catholicism along with music and other popular content. 

@newspaper_2
title: We must keep on building up our newspaper network.
subtitle: -1 resources
choose-if: resources >= 1
unavailable-subtitle: Not enough resources.
view-if: newspaper > 0 and newspaper <= 3
on-arrival: newspaper += 1; resources -= 1; new_middle_z += 5*(1 - dissent); old_middle_z += 3*(1 - dissent); workers_z += 3*(1 - dissent); pro_republic += 3*(1-dissent)*(democratization-2) if left_strength > right_strength; pro_republic += 3*(1-dissent)*(democratization-2) if left_labor_strength > right_strength

Our new newspaper network has a mix of popular and political programming, and is proving a success.

@newspaper_3
title: We must keep on building up our newspaper network. 
subtitle: It is now financially self-sufficient, and does not require any more resources.
view-if: newspaper > 3 and newspaper <= 5 and year <= 1931
on-arrival: newspaper += 1; new_middle_z += 5*(1 - dissent); old_middle_z += 4*(1 - dissent); workers_z += 2*(1 - dissent); rural_z += 3*(1 - dissent); catholics_z += 2*(1-dissent); pro_republic += 3*(1-dissent)*(democratization-2) if left_strength > right_strength; pro_republic += 3*(1-dissent)*(democratization-2) if left_labor_strength > right_strength

Our newspaper network has become a successful tool for spreading our message.

@newspaper_4
title: We should keep on supporting our newspaper network.
subtitle: As newspapers becomes more common, the novelty is beginning to wear off, and newspapers are becoming saturated in society. -1 resources
choose-if: resources >= 1
unavailable-subtitle: Not enough resources.
view-if: newspaper > 3 and (newspaper >= 6 or year >= 1932) and radio <= 9
on-arrival: newspaper += 1; resources -= 1; new_middle_z += 2*(1 - dissent); old_middle_z += 1*(1 - dissent); catholics_z += 2*(1 - dissent); rural_z += 1*(1 - dissent); pro_republic += 3*(1-dissent)*(democratization-2) if left_strength > right_strength; pro_republic += 3*(1-dissent)*(democratization-2) if left_labor_strength > right_strength

We are continuing to invest in our newspaper network. Both commercial and political radio are becoming more prevalent, making it harder to spread our message amidst the competition.
